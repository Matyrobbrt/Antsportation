plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.0'
group = 'com.matyrobbrt.antsportation' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'antsportation'

sourceSets.create 'data'
configurations.dataImplementation.extendsFrom(configurations.minecraft)

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    mappings channel: 'parchment', version: '2022.05.02-1.18.2'

    accessTransformers.from file('src/data/resources/accesstransformer.cfg')
    accessTransformers.from file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        all {
            mods {
                antsportation {
                    source sourceSets.main as SourceSet
                }
            }
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', 'simplegui'
        }

        client {}
        server {}
        gameTestServer {}

        data {
            args '--mod', 'antsportation', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
            mods {
                antsportation {
                    source sourceSets.data as SourceSet
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }
repositories {
    maven {
        name 'prog'
        url 'https://dvs1.progwml6.com/files/maven'
        content {
            includeGroup 'mezz.jei'
        }
    }
    maven { url 'https://modmaven.dev/' }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.19'

    compileOnly fg.deobf("mezz.jei:jei-1.18.2:${jei_version}:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:${jei_version}")

    runtimeOnly fg.deobf("mekanism:Mekanism:${mekanism_version}")

    dataImplementation sourceSets.main.output
}

void configureJarTask(Jar jar) {
    jar.configure {
        jar.manifest {
            attributes([
                    "Specification-Title"     : "Antsportation",
                    "Specification-Vendor"    : "Matyrobbrt, Learwin, Itskillerluc",
                    "Specification-Version"   : "1", // We are version 1 of ourselves
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.version,
                    "Implementation-Vendor"   : "Matyrobbrt",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }
}

jar {
    configureJarTask(jar)
}
tasks.jar.finalizedBy('reobfJar')